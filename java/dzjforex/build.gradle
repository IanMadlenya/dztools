apply plugin: 'java'
apply plugin: 'maven-publish'

group= 'com.jforex.dzplugin'
version = '0.9.5'
sourceCompatibility = 1.8
targetCompatibility = 1.8


description ="""
The Java plugin part for Zorro which lets you trade with Dukascopy
Project name: ${project.name}
"""

repositories {
   mavenCentral()
   maven {
       url "https://www.dukascopy.com/client/jforexlib/publicrepo"
   }
}

dependencies {
    compile 'org.aeonbits.owner:owner:1.0.9'
    compile 'commons-net:commons-net:3.3'

    compile files('libs/JForex-3-SDK-3.0.18.jar')
    compile files('libs/JForexUtils-0.9.8.jar')
    compile files("${System.env.JAVA_HOME}/jre/lib/javaws.jar")
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
    testCompile 'org.mockito:mockito-core:2.7.2'
    testCompile 'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1'
}

gradle.projectsEvaluated {
   tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Dukascopy-Zorro-Plugin',
                   'Implementation-Version': version
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyFatJarToPluginFolder(type: Copy, dependsOn:[fatJar]) {
    from "${buildDir}/libs/dzjforex-0.9.5.jar"
    into "${zorroPath}/Plugin/dukascopy"
}

task copyPluginFolder(type: Copy) {
    from ('Plugin') {
        into ('Plugin')
    }
    into "${zorroPath}"
}
